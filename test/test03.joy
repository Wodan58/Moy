%INCLUDE ../lib/stdlib.joy

'A ['B 'C 'D] [equal] some put_bool nl .

'B ['B 'C 'D] [equal] some put_bool nl .

10 [dup small_int] [] [pred dup pred] [+] binrec put_int nl .

DEFINE test ==
[
  [ 1 "one\n" putchars ]
  [ 2 "two\n" putchars ]
  [ pop "other\n" putchars ]
] case .

1 test .
2 test .
3 test .

DEFINE test == [ 1 + ] i .

2 test .

DEFINE test == [ pop 1 + ] x .

2 test .

DEFINE test == [ 2 3 + put nl ] dip .

1 test . .

DEFINE test == [2 3] [[+] [*] [-] [/]] construct .

test . . . . .

DEFINE prime ==
	2
	[ [ [dup * >] nullary  [rem 0 >] dip  and ] nullary ]
	[ succ ]
	while
	dup * < .

2 20 [dup put 32 putch dup prime put_bool nl succ] times .

DEFINE fib == [1 0] dip [swap [+] unary] times popd .

10 fib . .

DEFINE qsort1 == [dup small] [] [uncons [over over [first] unary2 >] split]
		 [enconcat] binrec .

[[2 3] [4 5] [6 7] [8 9] [1 2] [3 4] [5 6] [7 8] [9 0]] qsort1 .

1 2 3 4 5 [[+] unary] unary3 . . . .

1 2 3 4 5 [[+] unary] unary4 . . . . .

1 2 3 4 5 [+] binary . .

1 2 3 4 5 [+] ternary . .

DEFINE
    sum == 0 [+] fold;
    average == [sum] [size] cleave / .

[1.0 2.0 3.0] average .
[4.0 5.0 6.0] average .
[7.0 8.0 9.0] average .

0 ["true\n" putchars] ["false\n" putchars] branch .
1 ["true\n" putchars] ["false\n" putchars] branch .

[0] ["true\n" putchars] ["false\n" putchars] ifte .
[1] ["true\n" putchars] ["false\n" putchars] ifte .

DEFINE reverse_str == "" swap [swons_str] step_str;
       reverse_lst == [] swap [swons] step;
       reverse == [reverse_lst] [reverse_str] iflist.

[1 2 3 4 5 6 7 8 9] reverse .
"123456789" reverse .

DEFINE test ==
[
  [ [dup 1 =] "one\n" putchars ]
  [ [dup 2 =] "two\n" putchars ]
  [ "other\n" putchars ]
] cond .

1 test .
2 test .
3 test .

DEFINE swoncat == swap concat .

DEFINE from-to ==
	[] cons  [pop pop] swoncat
	[over over >] swap
	[ [dup succ] dip ]
	[cons]
	linrec;
from-to-list == [] from-to .

1 10 from-to-list .

1 10 [over over >] [pop pop []] [[dup succ] dip] [cons] linrec .

0 [dup 10 =] [] [dup put 32 putch succ] tailrec .

DEFINE g-fib == [dup small_int] [] [pred dup pred] [app2 +] genrec .

10 g-fib .

DEFINE
    cnr-ack ==
	[ [ [over null]  [popd succ] ]
	  [ [dup null]  [pop pred 1]  [] ]
	  [ [[dup pred swap] dip pred]  []  [] ] ]
	condnestrec .

3 4 cnr-ack .

[1 2 3 4 5 6 7 8 9] [1 +] map .

DEFINE test == [] [2 3 + 4 5 *] infra .

1 2 3 4 5 test . . . . . .

[1 2 3 4 5] [0] [+] primrec .

5 [1] [*] primrec_int .

"test" [""] [cons_str] primrec_str .

{ 1 2 3 } [{}] [cons_set] primrec_set put_set nl .

[1 2 3 4 5 6 7 8 9] [5 <] filter .

[1 2 3 4 5 6 7 8 9] [5 <] split . .

[1 2 3 4 5 6 7 8 9] [5 <] some put_bool nl .

[1 2 3 4 5 6 7 8 9] [5 <] all put_bool nl .

DEFINE treesample == [ [1 2 [3 4] 5 [[[6]]] 7 ] 8 ] .

[] treesample [swons] treestep .

treesample [dup *] [map] treerec . . .

DEFINE treemap == [] [map] treegenrec .

0 treesample [[dup] dip -] treemap .

MODULE rep

PRIVATE

duco == dup cons

PUBLIC

self == [duco] duco

END

rep.self .

LIBRA

    seqand  == [pop false] ifte;
    unops   == [? * + $];
    binops  == [| _];
    bin1ops == [|];
    bin2ops == [_];
    bin3ops == [] .

[+] 3 [pop [dup null not] [first unops in] seqand] [true] [false] ifte
put_bool nl .
