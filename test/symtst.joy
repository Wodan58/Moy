%INCLUDE stdlib.joy

DEFINE
unops == [ not succ pred fact first rest reverse_str i intern quit ] ;
swoncat == swap concat ;
shunt == [ swons ] step ;
shunt_str == [ swons_str ] step_str ;
sequand == [ pop false ] ifte ;
reverselist == [ ] swap shunt ;
reverse_str == "" swap shunt_str ;
reverse == [ [ ] ] [ "" ] iflist swap shunt ;
# fact == [ [ over null ] [ [ pop 1 ] dip ] [ [ dup pred ] dip i [ * ] dip ] ifte ] ;
fact == [ 1 1 ] dip [ dup [ * ] dip succ ] times pop ;
prime == 2 [ over over dup * > 2 pick 2 pick rem 0 > and ] [ succ ] while dupd dup * < ;
binops == [ and or + - * / = < > cons concat map filter ] ;
bin3ops == [ * / and cons ] ;
bin2ops == [ + - or concat ] ;
bin1ops == [ = < > ] ;
END
HIDE
new-infra == cons [ ] swap infra ;
X2Y == unswons [ [ [ dup unops in ] [ X2Y ] dip swap ] [ [ dup binops in ] [
X2Y X2Y ] dip swap ] [ swap ] ] cond ;
R2T == [ [ [ [ dup unops in ] [ [ ] cons ] dip swons ] [ [ dup binops in ] [ [
] cons cons ] dip swons ] [ [ dup list ] [ ] cons [ QUOTE ] swoncat ] [ [ ]
cons ] ] cond ] step ;
R2I == [ [ [ [ dup unops in ] swons ] [ [ dup binops in ] swons concat [ ] cons
] [ [ dup list ] [ ] cons [ QUOTE ] swoncat ] [ [ ] cons ] ] cond ] step ;
R2C == [ [ [ [ dup unops in ] [ [ ] cons ] dip swons ] [ [ dup binops in ] [ [
] cons cons ] dip swons ] [ [ dup list ] [ ] cons [ QUOTE ] swoncat ] [ ] ]
cond ] step ;
I2C == unswons [ [ [ dup unops in ] [ I2C [ ] cons ] dip swons ] [ [ dup [
QUOTE ] in ] [ unswons [ ] cons ] dip swons ] [ [ dup list ] I2C swap uncons
swapd I2C swons cons cons ] [ ] ] cond ;
C2T == [ dup list ] [ unswons [ [ [ dup unops in ] [ first C2T [ ] cons ] dip
swons ] [ [ dup [ QUOTE ] in ] swons ] [ [ uncons first [ C2T ] dip C2T [ ]
cons cons ] dip swons ] ] cond ] [ [ ] cons ] ifte ;
C2R == [ dup list ] [ unswons [ [ [ dup unops in ] swap first C2R ] [ [ dup [
QUOTE ] in ] pop first ] [ swap uncons first swap [ C2R ] dip C2R ] ] cond ] [
] ifte ;
C2P == [ dup list ] [ unswons [ [ [ dup unops in ] [ first C2P ] dip ] [ [ dup
[ QUOTE ] in ] pop first ] [ [ uncons first swap [ C2P ] dip C2P ] dip ] ] cond
] [ ] ifte ;
C2I == [ dup list ] [ unswons [ [ [ dup unops in ] [ first C2I ] dip swons ] [
[ dup [ QUOTE ] in ] swons ] [ [ uncons first [ C2I ] dip C2I ] dip swons
concat [ ] cons ] ] cond ] [ [ ] cons ] ifte ;
IN
Rev2Tre == R2T ;
Rev2Pol == reverselist [ X2Y ] new-infra rest ;
Rev2Inf == R2I ;
Rev2Cam == R2C ;
Pol2Rev == [ X2Y ] new-infra rest reverselist ;
Min2Tre == unswons M12T popd ;
Min2Rev == Min2Cam Cam2Rev ;
Min2Pol == Min2Cam Cam2Pol ;
Min2Inf == Min2Cam Cam2Inf ;
Min2Cam == unswons M12C popd ;
M42T == [ [ [ dup unops in ] [ unswons M42T [ ] cons ] dip swons ] [ [ dup [
QUOTE ] in ] [ unswons [ ] cons ] dip swons ] [ [ dup list ] unswons M12T popd
] [ [ ] cons ] ] cond ;
M42C == [ [ [ dup unops in ] [ unswons M42C [ ] cons ] dip swons ] [ [ dup [
QUOTE ] in ] [ unswons [ ] cons ] dip swons ] [ [ dup list ] unswons M12C popd
] [ ] ] cond ;
M32T == M42T [ over [ dup null not ] [ first bin3ops in ] sequand ] [ swap
uncons swapd unswons M32T rollupd [ ] cons cons cons ] [ ] ifte ;
M32C == M42C [ over [ dup null not ] [ first bin3ops in ] sequand ] [ swap
uncons swapd unswons M32C rollupd [ ] cons cons cons ] [ ] ifte ;
M22T == M32T [ over [ dup null not ] [ first bin2ops in ] sequand ] [ swap
uncons swapd unswons M22T rollupd [ ] cons cons cons ] [ ] ifte ;
M22C == M32C [ over [ dup null not ] [ first bin2ops in ] sequand ] [ swap
uncons swapd unswons M22C rollupd [ ] cons cons cons ] [ ] ifte ;
M12T == M22T [ over [ dup null not ] [ first binops in ] sequand ] [ swap
uncons swapd unswons M12T rollupd [ ] cons cons cons ] [ ] ifte ;
M12C == M22C [ over [ dup null not ] [ first binops in ] sequand ] [ swap
uncons swapd unswons M12C rollupd [ ] cons cons cons ] [ ] ifte ;
Inf2Rev == Inf2Cam Cam2Rev ;
Inf2Pol == Inf2Cam Cam2Pol ;
Inf2Cam == I2C popd ;
Cam2Tre == C2T ;
Cam2Rev == [ C2R ] new-infra ;
Cam2Pol == [ C2P ] new-infra ;
Cam2Inf == C2I ;
END

0 setautoput .
"\nTesting the 12 translators in the library  symlib.joy\n" putchars .
[ 2 3 * 4 5 + - ] Rev2Cam "(y(yii)(yii))" put_list nl .
[ 4 6 pred * 5 succ 7 + - fact ] Rev2Cam "(y(y(yi(yi))(y(yi)i)))" put_list nl .
[ true true not or false false and or ] Rev2Cam "(y(yb(yb))(ybb))" put_list nl .
[ 2 3 * 2 3 + succ = ] Rev2Cam "(y(yii)(y(yii)))" put_list nl .
[ 1 [ 2 3 ] cons 4 [ 5 6 ] cons concat ] Rev2Cam "(y(yi(y(ii)))(yi(y(ii))))" put_list nl .
[ [ 1 2 3 4 ] [ fact ] map ] Rev2Cam "(y(y(iiii))(y(y)))" put_list nl .
[ [ 6 7 8 9 ] [ prime ] filter ] Rev2Cam "(y(y(iiii))(y(y)))" put_list nl .
[ 2 3 * 4 5 + - ] Rev2Tre "(y(y(i)(i))(y(i)(i)))" put_list nl .
[ 4 6 pred * 5 succ 7 + - fact ] Rev2Tre "(y(y(y(i)(y(i)))(y(y(i))(i))))" put_list nl .
[ true true not or false false and or ] Rev2Tre "(y(y(b)(y(b)))(y(b)(b)))" put_list nl .
[ 2 3 * 2 3 + succ = ] Rev2Tre "(y(y(i)(i))(y(y(i)(i))))" put_list nl .
[ 1 [ 2 3 ] cons 4 [ 5 6 ] cons concat ] Rev2Tre "(y(y(i)(y(ii)))(y(i)(y(ii))))"
put_list nl .
[ [ 1 2 3 4 ] [ fact ] map ] Rev2Tre "(y(y(iiii))(y(y)))" put_list nl .
[ [ 6 7 8 9 ] [ prime ] filter ] Rev2Tre "(y(y(iiii))(y(y)))" put_list nl .
[ 2 3 * 4 5 + - ] Rev2Inf "(((iyi)y(iyi)))" put_list nl .
[ 4 6 pred * 5 succ 7 + - fact ] Rev2Inf "(y((iyyi)y(yiyi)))" put_list nl .
[ true true not or false false and or ] Rev2Inf "(((byyb)y(byb)))" put_list nl .
[ 2 3 * 2 3 + succ = ] Rev2Inf "(((iyi)yy(iyi)))" put_list nl .
[ 1 [ 2 3 ] cons 4 [ 5 6 ] cons concat ] Rev2Inf "(((iyy(ii))y(iyy(ii))))" put_list nl .
[ [ 1 2 3 4 ] [ fact ] map ] Rev2Inf "((y(iiii)yy(y)))" put_list nl .
[ [ 6 7 8 9 ] [ prime ] filter ] Rev2Inf "((y(iiii)yy(y)))" put_list nl .
[ 2 3 * 4 5 + - ] Rev2Pol "(yyiiyii)" put_list nl .
[ 4 6 pred * 5 succ 7 + - fact ] Rev2Pol "(yyyiyiyyii)" put_list nl .
[ true true not or false false and or ] Rev2Pol "(yybybybb)" put_list nl .
[ 2 3 * 2 3 + succ = ] Rev2Pol "(yyiiyyii)" put_list nl .
[ 1 [ 2 3 ] cons 4 [ 5 6 ] cons concat ] Rev2Pol "(yyi(ii)yi(ii))" put_list nl .
[ [ 1 2 3 4 ] [ fact ] map ] Rev2Pol "(y(iiii)(y))" put_list nl .
[ [ 6 7 8 9 ] [ prime ] filter ] Rev2Pol "(y(iiii)(y))" put_list nl .
[ - [ * 2 3 ] [ + 4 5 ] ] Cam2Inf "(((iyi)y(iyi)))" put_list nl .
[ fact [ - [ * 4 [ pred 6 ] ] [ + [ succ 5 ] 7 ] ] ] Cam2Inf "(y((iyyi)y(yiyi)))"
put_list nl .
[ or [ or true [ not true ] ] [ and false false ] ] Cam2Inf "(((byyb)y(byb)))" put_list
nl .
[ = [ * 2 3 ] [ succ [ + 2 3 ] ] ] Cam2Inf "(((iyi)yy(iyi)))" put_list nl .
[ concat [ cons 1 [ QUOTE [ 2 3 ] ] ] [ cons 4 [ QUOTE [ 5 6 ] ] ] ] Cam2Inf
"(((iyy(ii))y(iyy(ii))))" put_list nl .
[ map [ QUOTE [ 1 2 3 4 ] ] [ QUOTE [ fact ] ] ] Cam2Inf "((y(iiii)yy(y)))" put_list nl .
[ filter [ QUOTE [ 6 7 8 9 ] ] [ QUOTE [ prime ] ] ] Cam2Inf "((y(iiii)yy(y)))"
put_list nl .
[ [ [ 2 * 3 ] - [ 4 + 5 ] ] ] Inf2Pol "(yyiiyii)" put_list nl .
[ fact [ [ 4 * pred 6 ] - [ succ 5 + 7 ] ] ] Inf2Pol "(yyyiyiyyii)" put_list nl .
[ [ [ true or not true ] or [ false and false ] ] ] Inf2Pol "(yybybybb)" put_list nl .
[ [ [ 2 * 3 ] = succ [ 2 + 3 ] ] ] Inf2Pol "(yyiiyyii)" put_list nl .
[ [ [ 1 cons QUOTE [ 2 3 ] ] concat [ 4 cons QUOTE [ 5 6 ] ] ] ] Inf2Pol
"(yyi(ii)yi(ii))" put_list nl .
[ [ QUOTE [ 1 2 3 4 ] map QUOTE [ fact ] ] ] Inf2Pol "(y(iiii)(y))" put_list nl .
[ [ QUOTE [ 6 7 8 9 ] filter QUOTE [ prime ] ] ] Inf2Pol "(y(iiii)(y))" put_list nl .
[ - * 2 3 + 4 5 ] Pol2Rev "(iiyiiyy)" put_list nl .
[ fact - * 4 pred 6 + succ 5 7 ] Pol2Rev "(iiyyiyiyyy)" put_list nl .
[ or or true not true and false false ] Pol2Rev "(bbyybbyy)" put_list nl .
[ = * 2 3 succ + 2 3 ] Pol2Rev "(iiyiiyyy)" put_list nl .
[ concat cons 1 [ 2 3 ] cons 4 [ 5 6 ] ] Pol2Rev "(i(ii)yi(ii)yy)" put_list nl .
[ map [ 1 2 3 4 ] [ fact ] ] Pol2Rev "((iiii)(y)y)" put_list nl .
[ filter [ 6 7 8 9 ] [ prime ] ] Pol2Rev "((iiii)(y)y)" put_list nl .
[ - [ * 2 3 ] [ + 4 5 ] ] Cam2Rev i put_int nl .
[ fact [ - [ * 4 [ pred 6 ] ] [ + [ succ 5 ] 7 ] ] ] Cam2Rev i put_int nl .
[ or [ or true [ not true ] ] [ and false false ] ] Cam2Rev i put_bool nl .
[ = [ * 2 3 ] [ succ [ + 2 3 ] ] ] Cam2Rev i put_bool nl .
[ concat [ cons 1 [ QUOTE [ 2 3 ] ] ] [ cons 4 [ QUOTE [ 5 6 ] ] ] ] Cam2Rev i
put_list_int nl .
[ map [ QUOTE [ 1 2 3 4 ] ] [ QUOTE [ fact ] ] ] Cam2Rev i put_list_int nl .
[ filter [ QUOTE [ 6 7 8 9 ] ] [ QUOTE [ prime ] ] ] Cam2Rev i put_list_int nl .
[ [ [ 2 * 3 ] - [ 4 + 5 ] ] ] Inf2Rev i put_int nl .
[ fact [ [ 4 * pred 6 ] - [ succ 5 + 7 ] ] ] Inf2Rev i put_int nl .
[ [ [ true or not true ] or [ false and false ] ] ] Inf2Rev i put_bool nl .
[ [ [ 2 * 3 ] = succ [ 2 + 3 ] ] ] Inf2Rev i put_bool nl .
[ [ [ 1 cons QUOTE [ 2 3 ] ] concat [ 4 cons QUOTE [ 5 6 ] ] ] ] Inf2Rev i put_list_int
nl .
[ [ QUOTE [ 1 2 3 4 ] map QUOTE [ fact ] ] ] Inf2Rev i put_list_int nl .
[ [ QUOTE [ 6 7 8 9 ] filter QUOTE [ prime ] ] ] Inf2Rev i put_list_int nl .
[ - * 2 3 + 4 5 ] Pol2Rev i put_int nl .
[ fact - * 4 pred 6 + succ 5 7 ] Pol2Rev i put_int nl .
[ or or true not true and false false ] Pol2Rev i put_bool nl .
[ = * 2 3 succ + 2 3 ] Pol2Rev i put_bool nl .
[ concat cons 1 [ 2 3 ] cons 4 [ 5 6 ] ] Pol2Rev i put_list_int nl .
[ map [ 1 2 3 4 ] [ fact ] ] Pol2Rev i put_list_int nl .
[ filter [ 6 7 8 9 ] [ prime ] ] Pol2Rev i put_list_int nl .
[ 1 * 2 + 3 * 4 + 5 * 6 < 100 ] Min2Inf "((((iyi)y((iyi)y(iyi)))yi))" put_list nl .
[ 1 * 2 + 3 * 4 + 5 * 6 < 100 ] Min2Pol "(yyyiiyyiiyiii)" put_list nl .
[ 1 * 2 + 3 * 4 + 5 * 6 < 100 ] Min2Cam "(y(y(yii)(y(yii)(yii)))i)" put_list nl .
[ 1 * 2 + 3 * 4 + 5 * 6 < 100 ] Min2Tre "(y(y(y(i)(i))(y(y(i)(i))(y(i)(i))))(i))"
put_list nl .
[ 1 * 2 + 3 * 4 + 5 * 6 < 100 ] Min2Rev "(iiyiiyiiyyyiy)" put_list nl .
[ 1 * 2 + 3 * 4 + 5 * 6 < 100 ] Min2Rev i put_bool nl .
[ cons [ intern [ reverse_str [ cons_str 't "iuq" ] ] ] [ QUOTE [ ] ] ] Cam2Tre
"(y(y(y(y(c)(t))))(y()))" put_list nl .
[ cons [ intern [ reverse_str [ cons_str 't "iuq" ] ] ] [ QUOTE [ ] ] ] Cam2Inf
"((yy(cyt)yy()))" put_list nl .
[ cons [ intern [ reverse_str [ cons_str 't "iuq" ] ] ] [ QUOTE [ ] ] ] Cam2Pol
"(yyyyct())" put_list nl .
[ cons [ intern [ reverse_str [ cons_str 't "iuq" ] ] ] [ QUOTE [ ] ] ] Cam2Rev
"(ctyyy()y)" put_list nl .
[ cons [ intern [ reverse_str [ cons_str 't "iuq" ] ] ] [ QUOTE [ ] ] ] Cam2Rev i
"(y)" put_list nl .
[ cons [ intern [ reverse_str [ cons_str 't "iuq" ] ] ] [ QUOTE [ ] ] ] Cam2Rev i i .
