%INCLUDE stdlib.joy

DEFINE 
y-ack == [ [ [ [ 2 pick null ] pop popd succ ] [ [ over null ] [ pop pred 1 ]
	 dip i ] [ [ [ dup pred swap ] dip pred ] dip twice-i ] ] cond ] y ;
y == [ dup cons ] swoncat dup cons i ;
x-mcc91 == [ [ over 100 > ] [ pop 10 - ] [ [ 11 + ] dip twice-x ] ifte ] x ;
x-hamilhyp == [ [ over null ] [ pop pop ] [ dup [ [ dup rollup pred ] dip x ]
dip [ dupd cons ] dip [ swap pred ] dip x ] ifte ] x ;
x-fact == [ [ over null ] [ pop pop 1 ] [ [ dup pred ] dip x * ] ifte ] x ;
x-ack == [ [ [ [ 2 pick null ] pop popd succ ] [ [ over null ] [ pop pred 1 ]
dip x ] [ [ [ dup pred swap ] dip pred ] dip twice-x ] ] cond ] x ;
twice-x == dup [ x ] dip x ;
twice-i == dup [ i ] dip i ;
toggle == [ over over has_set ] [ [ not_set ] dip swons_set not_set ] [ swons_set ] ifte ;
swoncat == swap concat ;
r-mcc91 == [ dup 100 > ] [ 10 - ] [ 11 + r-mcc91 r-mcc91 ] ifte ;
r-hamilhyp == [ dup null ] [ pop ] [ dup rollup pred r-hamilhyp dupd cons swap
pred r-hamilhyp ] ifte ;
r-fact == [ dup null ] [ pop 1 ] [ dup pred r-fact * ] ifte ;
r-ack == [ [ [ over null ] popd succ ] [ [ dup null ] pop pred 1 r-ack ] [ [
dup pred swap ] dip pred r-ack r-ack ] ] cond ;
lr-hamilhyp == [ dup null ] [ pop ] [ dup rollup pred ] [ dupd cons swap pred
lr-hamilhyp ] linrec ;
lr-grayseq == [ dup null ] [ pop ] [ dup rollup pred ] [ dupd dup [ first swap
toggle ] dip cons swap pred lr-grayseq ] linrec ;
l-mcc91 == [ dup 100 > ] [ 10 - ] [ 11 + ] [ l-mcc91 ] linrec ;
dup2 == dupd dup swapd ;
cnr-mcc91 == [ [ [ dup 100 > ] [ 10 - ] ] [ [ 11 + ] [ ] [ ] ] ] condnestrec ;
cnr-hanoi == [ [ rolldown ] infra ] dip [ [ [ dup null ] [ pop pop ] ] [ [ dup2
[ [ rotate ] infra ] dip pred ] [ [ dup rest "(yy)" put_list ] dip [ [ swap ] infra ]
dip pred ] [ ] ] ] condnestrec ;
cnr-hamilhyp == [ [ [ dup null ] [ pop ] ] [ [ dup rollup pred ] [ dupd cons
swap pred ] [ ] ] ] condnestrec ;
cnr-grayseq == [ [ [ dup null ] [ pop ] ] [ [ dup rollup pred ] [ dupd dup [
first swap toggle ] dip cons swap pred ] [ ] ] ] condnestrec ;
cnr-fact == [ [ [ dup null ] [ pop 1 ] ] [ [ dup pred ] [ * ] ] ] condnestrec ;
cnr-even == [ [ [ dup 2 rem null ] [ pop true ] ] [ [ pop false ] ] ] condnestrec ;
cnr-ack == [ [ [ over null ] [ popd succ ] ] [ [ dup null ] [ pop pred 1 ] [ ]
] [ [ [ dup pred swap ] dip pred ] [ ] [ ] ] ] condnestrec ;
cnr-abs == [ [ [ dup 0 < ] [ 0 swap - ] ] [ [ ] ] ] condnestrec ;
clr-ack == [ [ [ over null ] [ popd succ ] ] [ [ dup null ] [ pop pred 1 ] [ ]
] [ [ [ dup pred swap ] dip pred ] [ clr-ack ] ] ] condlinrec ;
ack == [ [ [ dup 0 = ] [ pop 1 + ] ] [ [ over 0 = ] [ popd 1 - 1 swap ] [ ] ] [
[ dup rollup [ 1 - ] dip ] [ swap 1 - ack ] ] ] condlinrec ;
END

0 setautoput .
[ 0 1 2 3 4 5 6 ] [ r-fact ] map put_list_int nl .
[ -7 42 99 100 101 102 345 ] [ r-mcc91 ] map put_list_int nl .
[ [ 0 0 ] [ 0 1 ] [ 0 2 ] [ 0 3 ] [ 0 4 ] [ 0 5 ] ] [ i r-ack ] map put_list_int nl
[ [ 1 0 ] [ 1 1 ] [ 1 2 ] [ 1 3 ] [ 1 4 ] [ 1 5 ] ] [ i r-ack ] map put_list_int nl
[ [ 2 0 ] [ 2 1 ] [ 2 2 ] [ 2 3 ] [ 2 4 ] [ 2 5 ] ] [ i r-ack ] map put_list_int nl
[ [ 3 0 ] [ 3 1 ] [ 3 2 ] [ 3 3 ] [ 3 4 ] [ 3 5 ] ] [ i r-ack ] map put_list_int nl
[ [ 4 0 ] ] [ i r-ack ] map put_list_int nl .
[ ] 3 r-hamilhyp put_list_int nl .
[ ] 4 r-hamilhyp put_list_int nl .
[ ] 5 r-hamilhyp put_list_int nl .
[ 0 1 2 3 4 5 6 ] [ x-fact ] map put_list_int nl .
[ -7 42 99 100 101 102 345 ] [ x-mcc91 ] map put_list_int nl .
[ [ 3 0 ] [ 3 1 ] [ 3 2 ] [ 3 3 ] [ 3 4 ] [ 3 5 ] ] [ i x-ack ] map put_list_int nl .
[ [ 3 0 ] [ 3 1 ] [ 3 2 ] [ 3 3 ] [ 3 4 ] [ 3 5 ] ] [ i y-ack ] map put_list_int nl .
[ ] 5 x-hamilhyp put_list_int nl .
[ [ 0 0 ] [ 0 1 ] [ 0 2 ] [ 0 3 ] [ 0 4 ] [ 0 5 ] ] [ i swap ack ] map put_list_int nl
[ [ 1 0 ] [ 1 1 ] [ 1 2 ] [ 1 3 ] [ 1 4 ] [ 1 5 ] ] [ i swap ack ] map put_list_int nl
[ [ 2 0 ] [ 2 1 ] [ 2 2 ] [ 2 3 ] [ 2 4 ] [ 2 5 ] ] [ i swap ack ] map put_list_int nl
[ [ 3 0 ] [ 3 1 ] [ 3 2 ] [ 3 3 ] [ 3 4 ] [ 3 5 ] ] [ i swap ack ] map put_list_int nl
[ [ 4 0 ] ] [ i swap ack ] map put_list_int nl .
[ -7 42 99 100 101 102 345 ] [ l-mcc91 ] map put_list_int nl .
[ [ 3 0 ] [ 3 1 ] [ 3 2 ] [ 3 3 ] [ 3 4 ] [ 3 5 ] ] [ i clr-ack ] map put_list_int nl .
[ ] 5 lr-hamilhyp put_list_int nl .
{ } 4 toggle put_set nl .
{ 1 2 7 } 2 toggle put_set nl .
[ { } ] 3 lr-grayseq put_list_set nl .
[ { 3 } ] 3 lr-grayseq put_list_set nl .
[ { 1 2 3 } ] 3 lr-grayseq put_list_set nl .
[ ] 4 cnr-hamilhyp put_list_int nl .
3 4 cnr-ack put_int nl .
[ { } ] 3 cnr-grayseq put_list_set nl .
[ source destination temp ] 2 cnr-hanoi
[ S D T ] 5 cnr-hanoi nl .
[ 0 1 2 3 4 5 6 ] [ cnr-fact ] map put_list_int nl .
[ -7 42 99 100 101 102 345 ] [ cnr-mcc91 ] map put_list_int nl .
3 cnr-even put_bool nl .
4 cnr-even put_bool nl .
-5 cnr-abs put_int nl .
6 cnr-abs put_int nl .
