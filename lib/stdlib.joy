(*
    Print a list according to a descriptive string.
*)
LIBRA
ps == "stack: " putchars stack put nl ;
nl == '\n putch ;
space == 32 putch ;
get_data == [ dup null ] [ ] [ unswons ] ifte ;
put_list == 0 rollup
[ dup null_str not ]		# as long as the string is not empty
[ uncons_str			# read the first character
  [ [ [ '[ ]
      [ '] pop ]
      [ '( "[ " putchars ]
      [ ') "] " putchars pop ]
      [ 's putchars space ]
      [ 't put_str  space ]
      [ 'i put_int  space ]
      [ 'd put_dbl  space ]
      [ 'c put_char space ]
      [ 'b put_bool space ]
      [ 'y put_sym  space ]
      [ 'z put_set  space ]
      [ ]
    ] case
    get_data
  ] dip
] while pop pop ;		# continue
put_list_int  == "[ " putchars [ put_int  space ] step "]" putchars ;
put_list_str  == "[ " putchars [ put_str  space ] step "]" putchars ;
put_list_sym  == "[ " putchars [ put_sym  space ] step "]" putchars ;
put_list_dbl  == "[ " putchars [ put_dbl  space ] step "]" putchars ;
put_list_chr  == "[ " putchars [ putch    space ] step "]" putchars ;
put_list_char == "[ " putchars [ put_char space ] step "]" putchars ;
put_list_set  == "[ " putchars [ put_set  space ] step "]" putchars ;
put_list_bool == "[ " putchars [ put_bool space ] step "]" putchars ;
.
